@ECHO OFF
REM generate C header for Git revision

REM move to current directory (src_user/Script/Git)
cd /d %~dp0

REM move to src_user
cd ..\..

for /f "usebackq" %%a in (`"git log --pretty=format:%%H -1"`) do set git_rev_user=%%a
for /f "usebackq" %%a in (`"git log --pretty=format:%%h -1"`) do set git_rev_user_short=%%a

REM generate header
REM MEMO: このヒアドキュメントもどきの書き方はwineのcmd.exe(Windows 6.1.7601)だと動かない？
(
echo.#ifndef GIT_REVISION_CONFIG_H_
echo.#define GIT_REVISION_CONFIG_H_
echo.
echo./* This file is generated by src_user/Script/Git/revision.bat */
echo.#define C2A_GIT_REVISION_GENERATED
echo.
call ..\src_core\Script\Git\revision.bat
echo.#define GIT_REVISION_C2A_USER "%git_rev_user%"
echo.#define GIT_REVISION_C2A_USER_SHORT 0x%git_rev_user_short%
echo.
echo.#endif // GIT_REVISION_CONFIG_H_
) > Settings\git_revision_config.h

REM move to current directory (src_user/Script/Git)
cd /d %~dp0
